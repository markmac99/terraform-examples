# terraform to create ECS cluster

# create a VPC for the cluster
resource "aws_vpc" "ecs_vpc" {
  cidr_block = "172.128.0.0/16"
  tags = {
    Name = "ecsVPC"
  }
}
# create a subnet for the cluster
resource "aws_subnet" "ecs_subnet" {
  vpc_id                  = aws_vpc.ecs_vpc.id
  cidr_block              = "172.128.16.0/20"
  map_public_ip_on_launch = true
  tags = {
    Name = "ecs_subnet"
  }
}

# ECS requires internet connectivity. I've configured an Internet Gateway
# but you can also use a NAT Gateway or VPC Endpoint. 
resource "aws_internet_gateway" "ecs_igw" {
  vpc_id = aws_vpc.ecs_vpc.id

  tags = {
    Name = "ecs_igw"
  }
}

# route table that directs traffic to the IGW
resource "aws_default_route_table" "ecs_default_rtbl" {
  default_route_table_id = aws_vpc.ecs_vpc.default_route_table_id
#  vpc_id = aws_vpc.ecs_vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.ecs_igw.id
  }
  tags = {
    Name = "ecs_def_route"
  }  
}

# create a cluster called simpleecs
resource "aws_ecs_cluster" "simpleecs" {
  name = "simpleecs"
  tags = {
    "billingtag" = "workrelated"
  }
}

# declare the capacity provider type, in this case FARGATE
resource "aws_ecs_cluster_capacity_providers" "mycapprov" {
  cluster_name = aws_ecs_cluster.simpleecs.name

  capacity_providers = ["FARGATE", "FARGATE_SPOT"]
}

# defome a task
# the definition of the container it runs are in the webapp.json file
resource "aws_ecs_task_definition" "mytask" {
  family                = "mytask"
  container_definitions = file("step2/webapp.json")
  cpu                   = 256
  memory                = 512
  network_mode          = "awsvpc"
  tags = {
    billingtag = "workrelated"
  }
  requires_compatibilities = [
    "FARGATE",
  ]
  execution_role_arn = "arn:aws:iam::317976261112:role/ecsTaskExecutionRole"
  task_role_arn      = "arn:aws:iam::317976261112:role/ecsTaskExecutionRole"
  runtime_platform {
    operating_system_family = "LINUX"
  }
}

# define a service to run the task
resource "aws_ecs_service" "myservice" {
  name                    = "myservice"
  cluster                 = aws_ecs_cluster.simpleecs.id
  task_definition         = aws_ecs_task_definition.mytask.arn
  desired_count           = 1
  enable_ecs_managed_tags = true
  wait_for_steady_state   = false
  launch_type             = "FARGATE"
  network_configuration {
    assign_public_ip = true
    security_groups = [
      aws_security_group.ecssecgrp.id
    ]
    subnets = [
      aws_subnet.ecs_subnet.id
    ]
  }
}

# iam role for the task to use
resource "aws_iam_role" "ecstaskrole" {
  name               = "ecsTaskExecutionRole"
  assume_role_policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "ecs-tasks.amazonaws.com"
      },
      "Effect": "Allow",
      "Sid": ""
    }
  ]
}
EOF
}

# iam policies to attach to iam role 
resource "aws_iam_role_policy_attachment" "ecspolicy1" {
  role       = aws_iam_role.ecstaskrole.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}

# security group for the service to use
resource "aws_security_group" "ecssecgrp" {
  name        = "ecs-secgrp"
  description = "2022-03-28T19:20:35.764Z"
  vpc_id      = aws_vpc.ecs_vpc.id
  ingress = [
    {
      description      = ""
      from_port        = 80
      to_port          = 80
      protocol         = "tcp"
      cidr_blocks      = ["0.0.0.0/0"]
      ipv6_cidr_blocks = []
      prefix_list_ids  = []
      security_groups  = []
      self             = false
    }
  ]
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}
